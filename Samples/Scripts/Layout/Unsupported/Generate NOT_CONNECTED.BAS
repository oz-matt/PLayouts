

Attribute VB_Name = "Module1"

'*****
'GENERATE NOT_CONNECTED.bas		Thomas A. Frayda	 (tfrayda@detectionsys.com)
'*****
'This script searches the active document for pins that are not associated with a net and stores them in an array.  Fiducials, mounting holes, etc are also included
'(since a net must consist of 2 or more pins and it is possible to have the situation of only one actual unused component pin in a design).
'After all pins have been queried, a file in the format generated by the ECO process is created and the stored component pins are written to this file.
'The ECO file is read back in and then deleted.  This process places all unused pins together on a net called NOT_CONNECTED (keeping with Specctra convention).
'Test points can now be routed from any of the pins on this net as needed.
'*****
'REVISIONS (yymmdd)

'981218		Eliminated the ignoring of mechanical items such as fiducials or moutning holes.  Considering these items as part of the net provides the ability to handle
'			 a design situation where only one component pin is unused.

'981211		Initial release of program
			
'*****
Dim ListPins$(10000)
Sub Main
	index = 0
	PinCount = 0
	Dim nextPin As Object

	Begin Dialog UserDialog 760,280
		Text 80,14,550,14,"GENERATE NOT_CONNECTED NET FOR TEST POINT PLACEMENT ON UNUSED PINS",.Text1
		Text 320,35,80,14,"REV: 981218"
		Text 320,49,80,14,"T. A.  Frayda",.Text2
		OKButton 220,238,80,28
		CancelButton 420,238,80,28
		Text 20,147,720,28,"After running this script, it is advisable to select view\nets, add NOT_CONNECTED and set its color-by-net to something that will stand out.  Test points can then easily be manually routed from any pin on this net.",.Text3
		Text 20,182,720,28,"Be certain that the Ignore Unused Pins Net is selected and the net name NOT_CONNECTED is specified whenever a Compare Netlist or Check ASCII operation is performed to avoid disrupting previously placed test points.",.Text4
		Text 20,112,720,28,"Mechanical items (fiducials, mounting holes, etc) are also considered as part of this net to provide the ability to handle a design situation where only one component pin is unused. ",.Text5
		Text 20,70,720,28,"This script selects all component pins not associated with a net and ties them together on a net called NOT_CONNECTED in keeping with Specctra convention.",.Text6
	End Dialog
	Dim dlg1 As UserDialog
	Dialog dlg1
	For Each nextPin In ActiveDocument.GetObjects(3, "", False)															'select a pin in active pcb design
		If (nextPin.Net Is Nothing)  Then																				'if associated net name is nothing, get component name of that pin
				ListPins$(index) = nextPin.Name																		'store comp.pin in array
				index = index + 1
		Else
		End If		
	Next nextPin																									'check next pin in design
	DBFileName$ = ActiveDocument.FullName																		'get full name of active design
	PinCount= index

	FileName = Path & "NOT_CONNECTED.eco"																		
	Open filename For Output As #1																					'open file named NOT_CONNECTED.eco in working directory
	Print #1, "*PADS-ECO-MILS*"																					'write header information
	Print #1,  "*PADS-ECO* ENGINEERING CHANGE ORDER REPORT"
	Print #1, "*REMARK* -- "; DBFilename$
	Print #1, "*NET*"																								'create new net
	Print #1, "*SIGNAL* NOT_CONNECTED 10"																		'assign new net name NOT_CONNECTED, set width = 10
	Print #1
		For outindex = 0 To index-1
			Print #1, ListPins$(outindex)																				'write comp.pin info stored in the array
		Next outindex
	Print #1, "*END*"
	Close #1																										'close the file
	ActiveDocument.ImportECOFile (FileName)																		'import  NOT_CONNECTED.eco	
	Kill FileName																									'delete NOT_CONNECTED.eco
	MsgBox PinCount& " pins were placed on the NOT_CONNECTED net."
End Sub
